grammar org.sqlproc.model.ProcessorModel with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate processorModel "http://www.sqlproc.org/model/ProcessorModel"
import "http://www.eclipse.org/Xtext/Xbase/XAnnotations"

Artifacts: {Artifacts}
	( properties+=Property 
	| pojos+=PojoDefinition 
	| tables+=TableDefinition
    | procedures+=ProcedureDefinition 
    | functions+=FunctionDefinition
    | packages+=Package
    )*;

ValueType: value = STRING | number = INT | id = QualifiedName;
DatabaseCatalogAssignement: dbCatalog=QualifiedName;
DatabaseSchemaAssignement: dbSchema=QualifiedName;
DatabaseTypeAssignement: dbType=ValidID;
DatabaseMetaInfoAssignement: dbMetaInfo=ValueType;
DriverMetaInfoAssignement: dbDriverInfo=ValueType;
DriverMethodOutputAssignement: driverMethod=ValidID '->' callOutput=ValueType;
SqlTypeAssignement: sqlType=ValueType '->' (':')? type=JvmParameterizedTypeReference;
ColumnTypeAssignement: dbColumn=ValidID '->' (':')? type=JvmParameterizedTypeReference;
ShowColumnTypeAssignement: dbColumn=ValidID '->' type=ValueType;
TableAssignement: dbTable=ValidID '->' newName=ValidID;
JoinTableAssignement: dbTable=ValidID ('->' dbTables+=ValidID)+;
ColumnAssignement: dbColumn=ValidID '->' newName=ValidID;
ImportAssignement: dbColumn=ValidID '->' pkTable=ValidID ('->' pkColumn=ValidID)?;
ExportAssignement: dbColumn=ValidID '->' fkTable=ValidID ('->' fkColumn=ValidID)?;
InheritanceAssignement: discriminator=ValidID '->' dbTable=ValidID ('->' dbColumns+=ValidID)+;
ManyToManyAssignement: pkColumn=ValidID '->' pkTable=ValidID ('->' fkColumn=ValidID)?;
DebugLevelAssignement: debug=ValidID (scope=ValidID)?;
ProcedurePojoAssignement: dbProcedure=ValidID '->' (':')? pojo=JvmParameterizedTypeReference;
FunctionPojoAssignement: dbFunction=ValidID '->' (':')? pojo=JvmParameterizedTypeReference;
ImplementsAssignement: ':' toImplement=JvmParameterizedTypeReference ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?;
ExtendsAssignement: ':' toExtends=JvmParameterizedTypeReference ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?;
ImplementsAssignementGenerics: ':' toImplement=JvmParameterizedTypeReference ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?;
ExtendsAssignementGenerics: ':' toExtends=JvmParameterizedTypeReference ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?;

PropertyCondition:
	'{' name=ValidID '=' value=ValueType '}'
;

Property: (condition=PropertyCondition)?
	(name='resolve-pojo-on'
	|name='resolve-pojo-off'
	|name='database-' database=DatabaseProperty
	|name='pojogen-' pojogen=PojogenProperty
	|name='metagen-' metagen=MetagenProperty
	|name='daogen-' daogen=DaogenProperty
	|name='replace-all-regex' replaceId=ValidID regex=ValueType
	|name='replace-all-replacement' replaceId=ValidID replacement=ValueType
	|name='compress-meta-directives'
	) ';';

DatabaseProperty:
	(name='is-online'
	|name='is-offline'
	|name='has-url' dbUrl=ValueType
	|name='login-username' dbUsername=ValueType
	|name='login-password' dbPassword=ValueType
	|name='in-catalog' dbCatalog=DatabaseCatalogAssignement
	|name='active-schema' dbSchema=DatabaseSchemaAssignement
	|name='jdbc-driver' (=> ':' dbDriverx=JvmParameterizedTypeReference|dbDriver=ValueType)
	|name='ddl-create' dbExecuteBefore=ValueType
	|name='ddl-drop' dbExecuteAfter=ValueType
	|name='index-types' dbIndexTypes=ValueType
	|name='skip-indexes'
	|name='skip-functions-procedures'
	|name='skip-check-constraints'
	|name='is-of-type' dbType=DatabaseTypeAssignement
	|name='show-database-info' dbMetaInfo=DatabaseMetaInfoAssignement
	|name='show-driver-info' dbDriverInfo=DriverMetaInfoAssignement
	|name='show-driver-output' (dbMethodsCalls+=DriverMethodOutputAssignement)+
	|name='debug-level' debug=DebugLevelAssignement
	|name='take-comments'
	|name='lowercase-names'
	|name='uppercase-names'
	);

PojogenProperty:
	(name='types-sqltypes' (sqlTypes+=SqlTypeAssignement)+
	|name='types-in-table' dbTable=ValidID (sqlTypes+=SqlTypeAssignement)+
	|name='types-for-columns' dbTable=ValidID (columnTypes+=ColumnTypeAssignement)+
	|name='types-for-procedure' dbProcedure=ValidID (columnTypes+=ColumnTypeAssignement)+
	|name='types-for-function' dbFunction=ValidID (columnTypes+=ColumnTypeAssignement)+
	|name='show-type-for-column' dbTable=ValidID columnType=ShowColumnTypeAssignement
	|name='show-type-for-procedure' dbProcedure=ValidID columnType=ShowColumnTypeAssignement
	|name='show-type-for-function' dbFunction=ValidID columnType=ShowColumnTypeAssignement
	|name='ignore-tables' (dbTables+=ValidID)+
	|name='only-tables' (dbTables+=ValidID)+
	|name='not-abstract-tables' (dbTables+=ValidID)+
	|name='join-tables' (joinTables+=JoinTableAssignement)+
	|name='ignore-columns' dbTable=ValidID (dbColumns+=ValidID)+
	|name='required-columns' dbTable=ValidID (dbColumns+=ValidID)+
	|name='not-required-columns' dbTable=ValidID (dbColumns+=ValidID)+
	|name='create-columns' dbTable=ValidID (columnTypes+=ColumnTypeAssignement)+
	|name='rename-tables' (tables+=TableAssignement)+
	|name='rename-columns' dbTable=ValidID (columns+=ColumnAssignement)+
	|name='ignore-one-to-many' dbTable=ValidID (exports+=ExportAssignement)+ // exports
	|name='ignore-many-to-one' dbTable=ValidID (imports+=ImportAssignement)+ // imports
	|name='inherit-many-to-one' dbTable=ValidID (imports+=ImportAssignement)+ // imports
	|name='create-one-to-many' dbTable=ValidID (exports+=ExportAssignement)+ // exports
	|name='create-many-to-one' dbTable=ValidID (imports+=ImportAssignement)+ // imports
	|name='table-many-to-many' dbTable=ValidID (many2s+=ManyToManyAssignement)+
	|name='inherit-discriminator' dbTable=ValidID dbColumn=ValidID (inheritance+=InheritanceAssignement)+
	|name='generate-methods' (methods+=ValidID)+
	|name='generate-operators' (operatorsSuffix=ValidID)?
	|name='implements-interfaces' toImplements=ImplementsAssignement 
	|name='extends-class' toExtends=ExtendsAssignement
	|name='implements-interfaces-generics' toImplementsGenerics=ImplementsAssignementGenerics 
	|name='extends-class-generics' toExtendsGenerics=ExtendsAssignementGenerics
	|name='generate-wrappers'
	|name='generate-validation-annotations'
	|name='preserve-foreign-keys' (dbTables+=ValidID)*
	|name='make-it-final'
	|name='version-column' version=ValidID ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?
	|name='debug-level' debug=DebugLevelAssignement
	|name='pojos-for-procedures' (procPojos+=ProcedurePojoAssignement)+
	|name='pojos-for-functions' (funPojos+=FunctionPojoAssignement)+
	|name='active-filter' activeFilter=ValueType
	|name='package' pckg=QualifiedName
	|name='enum-for-check-constraints' enumName=ValidID (dbCheckConstraints+=ValidID)+
	);

MetaTypeAssignement: dbColumn=ValidID '->' type=ValidID ('->' extension=ValidID)?;

MetagenProperty:
	(name='global-sequence' sequence=ValidID (type=ValidID)? ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?
	|name='table-sequence' dbTable=ValidID sequence=ValidID (type=ValidID)?
	|name='global-identity' (identity=ValidID (type=ValidID)?)? ('+' (dbTables+=ValidID)+)? ('-' (dbNotTables+=ValidID)+)?
	|name='table-identity' dbTable=ValidID identity=ValidID (type=ValidID)?
	|name='column-meta-type' dbTable=ValidID (metaTypes+=MetaTypeAssignement)+
	|name='statement-meta-type' dbStatement=ValidID (metaTypes+=MetaTypeAssignement)+
	|name='make-it-final'
	|name='like-columns' (dbTable=ValidID (dbColumns+=ValidID)+)?
	|name='not-like-columns' (dbTable=ValidID (dbColumns+=ValidID)+)?
	|name='generate-sequences'
	|name='generate-identities'
	|name='generate-idgenerators'
	|name='generate-default-idgenerators'
	|name='function-result' dbFunction=ValidID type=ValidID
	|name='function-result-set' dbFunction=ValidID dbTable=ValidID
	|name='procedure-result-set' dbProcedure=ValidID dbTable=ValidID
	|name='debug-level' debug=DebugLevelAssignement
	|name='generate-operators'
	|name='optimize-insert' (dbTables+=ValidID)*
	|name='optional-features' dbStatement=ValidID (optionalFeatures+=ValidID)+
	|name='active-filter' activeFilter=ValueType
	);

DaogenProperty:
	(name='ignore-tables' (dbTables+=ValidID)+
	|name='only-tables' (dbTables+=ValidID)*
	|name='implements-interfaces' toImplements=ImplementsAssignement
	|name='extends-class' toExtends=ExtendsAssignement
	|name='implements-interfaces-generics' toImplementsGenerics=ImplementsAssignementGenerics 
	|name='extends-class-generics' toExtendsGenerics=ExtendsAssignementGenerics
	|name='make-it-final'
	|name='function-result' dbFunction=ValidID (':')? resultType=JvmParameterizedTypeReference
	|name='debug-level' debug=DebugLevelAssignement
	|name='active-filter' activeFilter=ValueType
	|name='package' pckg=QualifiedName
	);

PojoDefinition:
	'pojo' name=ValidID (class=(QualifiedName) | ':' classx=JvmParameterizedTypeReference) ';';

TableDefinition:
	'table' name=ValidID table=ValidID ';';

ProcedureDefinition:
	'procedure' name=ValidID table=ValidID ';';

FunctionDefinition:
	'function' name=ValidID table=ValidID ';';

PackageDirective: 
{PackageDirectiveImplementation} '#Implementation' LPAREN implementation=QualifiedName RPAREN
;

Package:
	directives+=PackageDirective*
	'package' name=(QualifiedName)
	'{'
		importSection=XImportSection?
		elements+=AbstractEntity*
	'}'
;

AbstractEntity:
	Implements | Extends | AnnotatedEntity	
;

ImplementsExtendsDirective: 
	{ImplementsExtendsDirectiveGenerics} '#Generics'
	| {ImplementsExtendsDirectiveOnlyPojos} '#OnlyPojos' onlyPojos+=[PojoEntity|ValidID]*
	| {ImplementsExtendsDirectiveOnlyDaos} '#OnlyDaos' onlyDaos+=[DaoEntity|ValidID]*
	| {ImplementsExtendsDirectiveExceptPojos} '#ExceptPojos' exceptPojos+=[PojoEntity|ValidID]*
	| {ImplementsExtendsDirectiveExceptDaos} '#ExceptDaos' exceptDaos+=[DaoEntity|ValidID]*
;

Implements:
	directives+=ImplementsExtendsDirective*
	'implements' implements = JvmParameterizedTypeReference
;

Extends:
	directives+=ImplementsExtendsDirective*
	'extends' extends = JvmParameterizedTypeReference
;

AnnotatedEntity:
	annotations+=Annotation*
	entity=Entity 
;

Entity:
	PojoEntity | EnumEntity | DaoEntity
;

DirectiveProperties:
	features+=[PojoAttribute|ValidID] (COMMA features+=[PojoAttribute|ValidID])*
;

PojoDirective: 
	{PojoDirectiveToString} '#ToString' LPAREN proplist=DirectiveProperties RPAREN
	| {PojoDirectiveIndex} '#Index' LPAREN index=INT COMMA proplist=DirectiveProperties RPAREN
	| {PojoDirectiveOperators} '#Operators' (LPAREN operatorsSuffix=ValidID RPAREN)?
	| {PojoDirectiveSerializable} '#Serializable' LPAREN sernum=INT RPAREN
	| {PojoDirectiveDiscriminator} '#InheritanceChild' LPAREN discriminator=(ValidID) RPAREN
	| {PojoDirectiveEquals} '#Equals' LPAREN proplist=DirectiveProperties RPAREN
	| {PojoDirectiveHashCode} '#HashCode' LPAREN proplist=DirectiveProperties RPAREN
;

PojoEntity:
	directives+=PojoDirective*
	(final?='final')? (abstract?='abstract')? 
	'#Pojo' name=ValidID ('extends' superType=JvmParameterizedTypeReference)?
	'{'
		attributes+=PojoAttribute*
		procedures+=PojoProcedure*
	'}'
;

PojoAttributeDirective: 
	{PojoAttributeDirectiveRequired} '#Required'
	| {PojoAttributeDirectivePrimaryKey} '#PrimaryKey'
	| {PojoAttributeDirectiveDiscriminator} '#InheritanceDiscriminator'
	| {PojoAttributeDirectiveIndex} '#Index' LPAREN index=INT RPAREN
	| {PojoAttributeDirectiveVersion} '#Version'
	| {PojoAttributeDirectiveUpdateCol} '#UpdateCol' LPAREN updateColumn1=[PojoAttribute|ValidID] COMMA updateColumn2=[PojoAttribute|ValidID] RPAREN
	| {PojoAttributeDirectiveCreateCol} '#CreateCol' LPAREN createColumn1=[PojoAttribute|ValidID] COMMA createColumn2=[PojoAttribute|ValidID] RPAREN
	| {PojoAttributeDirectiveToInit} '#ToInit'
	| {PojoAttributeDirectiveEnumInit} '#EnumInit'
	| {PojoAttributeDirectiveIsDef} '#IsDef'
	| {PojoAttributeDirectiveEnumDef} '#EnumDef'
;

PojoAttribute:
	annotations+=Annotation* directives+=PojoAttributeDirective*
	(final?='final'?) (static?='static'?) 
	kind=('#Attr' | '#Simple') //modifiers+=Modifier* 
	(type=JvmTypeReference)? name=ValidID ('=' initExpr=XExpression)?
;

PojoProcedure:
	annotations+=Annotation*
	(final?='final')? (=> static?='static'?)
	'#Proc' //modifiers+=Modifier*
	(type=JvmTypeReference)? name=ValidID 
	LPAREN (params+=FullJvmFormalParameter (COMMA params+=FullJvmFormalParameter)*)? RPAREN
	body=XBlockExpression
;

EnumDirective:
	{EnumDirectiveSerializable} '#Serializable' LPAREN sernum=INT RPAREN
;

EnumEntity:
	directives+=EnumDirective*
	(final?='final')?
	'#Enum' name=ValidID 
	'{'
		attribute=EnumAttribute
		procedures+=PojoProcedure*
	'}'
;

EnumAttributeValue: name=ValidID '=' value = XExpression;

EnumAttributeDirective:
	{EnumAttributeDirectiveValues} '#Values' LPAREN values+=EnumAttributeValue (COMMA values+=EnumAttributeValue)* RPAREN
;

EnumAttribute:
	directives+=EnumAttributeDirective 
	(final?='final'?) (static?='static'?) 
	'#Attr' type=JvmTypeReference name=ValidID
;


DaoDirectiveParameters:
	out=JvmParameterizedTypeReference (COMMA ins+=JvmParameterizedTypeReference)*
;

DescendantAssignment:
	value = ValueType '=' descendant=JvmParameterizedTypeReference
;

FunProcType :
	{FunctionCallQuery} "#FunctionCallQuery" | 
	{ProcedureCallQuery} "#ProcedureCallQuery" | 
	{FunctionCall} "#FunctionCall" | 
	{ProcedureUpdate} "#ProcedureUpdate" | 
	{FunctionQuery} "#FunctionQuery"
;

DaoDirective:
	{DaoDirectiveSerializable} '#Serializable' LPAREN sernum=INT RPAREN
	| {DaoDirectiveDiscriminator} '#InheritanceHandler' LPAREN ancestor=[PojoAttribute|ValidID] (COMMA descendants+=DescendantAssignment)+ RPAREN
	| {DaoDirectiveCrud} '#CRUD' (=> LPAREN pojo=[PojoEntity|ValidID] RPAREN)?
	| {DaoDirectiveQuery} '#Query' (=> LPAREN pojo=[PojoEntity|ValidID] RPAREN)?
	| {DaoFunProcDirective} type=FunProcType (=> LPAREN paramlist=DaoDirectiveParameters RPAREN)?
	| {DaoDirectivePojo} '#Pojo' (=> LPAREN pojo=[PojoEntity|ValidID] RPAREN)
;

DaoEntity:
	directives+=DaoDirective*
	(final?='final')? (abstract?='abstract')? 
	'#Dao' name=ValidID ('extends' superType=JvmParameterizedTypeReference)?
	'{'
		attributes+=PojoAttribute*
		procedures+=PojoProcedure*
	'}'
;

//Modifier: {Modifier}
//	(abstract?='abstract'? & static?='static'? & final?='final'?)
//;

AnnotationDirective: 
	{AnnotationDirectiveConflict} '#Conflict'
	| {AnnotationDirectiveStatic} '#Static'
	| {AnnotationDirectiveConstructor} '#Constructor'
	| {AnnotationDirectiveStandard} '#Standard'
	| {AnnotationDirectiveSetter} '#Setter'
	| {AnnotationDirectiveGetter} '#Getter'
	| {AnnotationDirectiveAttribute} '#Attribute'
;

Annotation:
	directives+=AnnotationDirective* annotation=XAnnotation
;

terminal LPAREN: '(';
terminal RPAREN: ')';
terminal COMMA: ',';
